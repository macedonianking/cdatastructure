import("//build/config/android/config.gni")

rebased_android_sdk_jar = rebase_path(android_sdk_jar, root_build_dir)
rebased_android_sdk_build_tools =
              rebase_path(android_sdk_build_tools, root_build_dir)
android_default_aapt_path = "$rebased_android_sdk_build_tools/aapt${executable_suffix}"

# 生成*.build_config文件
template("write_build_config") {
  type = invoker.type

  action(target_name) {
    script = "//build/android/gyp/write_build_config.py"

    forward_variables_from(invoker, 
                        [ 
                          "testonly",
                          "possible_config_deps",
                          "deps",
                          "build_config",
                        ])
    if (!defined(possible_config_deps)) {
      possible_config_deps = []
    }
    if (!defined(deps)) {
      deps = []
    }

    direct_deps_configs = []
    foreach(dep, possible_config_deps) {
      _target = get_label_info(dep, "label_no_toolchain")
      _target_name = get_label_info(_target, "name")
      _target_build_config = "${_target}__build_config"
      deps += [ _target_build_config ]
      _target_build_config_path = get_label_info(_target, "target_gen_dir") 
        + "${_target_name}.build_config"
      direct_deps_configs += [ _target_build_config ]
    }

    rebased_direct_deps_configs = rebase_path(direct_deps_configs, root_build_dir)
    rebased_build_config = rebase_path(build_config, root_build_dir)

    sources = [] + direct_deps_configs
    outputs = [
      build_config,
    ]

    args = [
      "--type",
      type,
      "--build-config",
      rebased_build_config,
      "--deps-configs=$rebased_direct_deps_configs",
    ]

    if (type == "android_resources") {
      assert(defined(invoker.resources_zip))
      args += [
        "--resources-zip",
        rebase_path(invoker.resources_zip, root_build_dir)
      ]
      assert(defined(invoker.resource_dirs))
      rebased_resource_dirs = rebase_path(invoker.resource_dirs, root_build_dir)
      args += [
        "--resource-dirs=$rebased_resource_dirs",
      ]
      if (defined(invoker.android_manifest)) {
        args += [
          "--android-manifest",
          rebase_path(invoker.android_manifest, root_build_dir)
        ]
      }
    } else if (type == "java_library" || type == "java_binary") {
      assert(defined(invoker.supports_android) && defined(invoker.supports_android))
      if (defined(invoker.java_sources_file)) {
        args += [
          "--java-sources-file",
          rebase_path(invoker.java_sources_file, root_build_dir),
        ]
      }

      if (defined(invoker.jar_path)) {
        args += [
          "--jar-path",
          rebase_path(invoker.jar_path, root_build_dir),
        ]
      }
      if (invoker.supports_android) {
        args += [
          "--supports-android",
        ]
      }
      if (invoker.requires_android) {
        args += [
          "--requires-android",
        ]
      }
    }
  }

}

# 生成资源包
template("process_resources") {
  
  zip_path = invoker.zip_path
  srcjar_path = invoker.srcjar_path
  r_text_path = invoker.r_text_path
  android_manifest = invoker.android_manifest
  build_config = invoker.build_config

  action(target_name) {
    forward_variables_from(invoker, 
                          [
                            "deps",
                            "visibility",
                          ])
    script = "//build/android/gyp/process_resources.py"
    outputs = [
      zip_path,
      srcjar_path,
      r_text_path,
    ]
    inputs = [
      build_config,
      android_manifest,
    ]

    depfile = "$target_gen_dir/$target_name.d"

    _rebased_all_resource_dirs = rebase_path(invoker.resource_dirs, root_build_dir)
    _rebased_android_sdk_jar = rebased_android_sdk_jar
    _android_aapt_path = android_default_aapt_path
    _rebased_android_manifest = rebase_path(android_manifest, root_build_dir)
    rebase_build_config = rebase_path(build_config, root_build_dir)

    args = [
      "--depfile",
      rebase_path(depfile, root_build_dir),
      "--resource-dirs=$_rebased_all_resource_dirs",
      "--resource-zip-out",
        rebase_path(zip_path, root_build_dir),
      "--srcjar-out",
      rebase_path(srcjar_path, root_build_dir),
      "--r-text-out",
      rebase_path(r_text_path, root_build_dir),
      "--android-sdk-jar",
      _rebased_android_sdk_jar,
      "--aapt-path",
      _android_aapt_path,
      "--android-manifest",
      _rebased_android_manifest,
      "--dependencies-res-zips=@FileArg($rebase_build_config:resources:dependency_zips)",
    ]

    # 跳过一些强制性的检查，如用paddingStart代码paddingLeft
    if (defined(invoker.v14_skip) && invoker.v14_skip) {
      args += [ "--v14-skip" ]
    }
  }

}

# 编译java文件的目标
template("compile_java") {
  set_sources_assignment_filter([])
  forward_variables_from(invoker, [ "testonly" ])

  assert(defined(invoker.jar_path))

  _build_config = invoker.build_config
  _rebased_build_config = rebase_path(_build_config, root_build_dir)
  assert([ _rebased_build_config ] != [])

  _java_srcjars = []
  if (defined(invoker.srcjars)) {
    _java_srcjars += invoker.srcjars
  }

  _javac_target_name = "${target_name}__javac"
  _final_target_name = target_name

  # 生成的jar的路径
  _javac_jar_path = invoker.jar_path

  action(_javac_target_name) {
    forward_variables_from(invoker, 
                           [
                            "testonly",
                            "deps",
                           ])

    script="//build/android/gyp/javac.py"
    depfile = "$target_gen_dir/$target_name.d"

    outputs = [
      _javac_jar_path,
      _javac_jar_path + ".md5.stamp",
    ]

    sources = invoker.java_files

    _rebased_jar_path = rebase_path(_javac_jar_path, root_build_dir)
    _rebased_dep_file = rebase_path(depfile, root_build_dir)
    _rebased_classpath = rebase_path(invoker.classpath, root_build_dir)
    _rebased_java_srcjars = rebase_path(_java_srcjars, root_build_dir)

    args = [
      "--depfile",
      _rebased_dep_file,
      "--jar-path",
      _rebased_jar_path,
      "--java-srcjars=$_rebased_java_srcjars",
      "--classpath=$_rebased_classpath",
    ]

    if (defined(invoker.requires_android) && invoker.requires_android) {
      args += [
        "--requires-android"
      ]
    }
    if (defined(invoker.supports_android) && invoker.supports_android) {
      args += [
        "--supports-android"
      ]
    }

    if (invoker.java_files != []) {
      args += [ "@" + rebase_path(invoker.java_sources_file, root_build_dir) ]
    }
  }

  # 最终目标
  group(_final_target_name) {
    deps = [
      ":$_javac_target_name",
    ]
  }
}

template("java_library_impl") {
  # testonly属性设置到所有的目标
  set_sources_assignment_filter([])
  forward_variables_from(invoker, [ "testonly" ])

  assert(defined(invoker.java_files) || defined(invoker.srcjars) ||
         defined(invoker.srcjars_deps))
  _final_deps = []
  _base_path = "$target_gen_dir/$target_name"

  _jar_name = target_name
  if (defined(invoker.jar_name)) {
    _jar_name = invoker.jar_name
  }
  target_dir_name = get_label_info(":$target_name", "dir")
  _jar_path = "$root_out_dir/lib.java$target_dir_name/$_jar_name.jar"
  if (defined(invoker.jar_path)) {
    _jar_path = invoker.jar_path
  }

  _java_files = []
  if (defined(invoker.java_files)) {
    _java_files += invoker.java_files
  }

  if (_java_files != []) {
    # 源文件的列表
    _java_sources_file = "$_base_path.source"
    write_file(_java_sources_file, rebase_path(_java_files, root_build_dir))
  }

  _supports_android = defined(invoker.supports_android) && invoker.supports_android
  _requires_android = defined(invoker.requires_android) && invoker.requires_android
  if (_supports_android) {
    _dex_path = "$_base_path.dex.jar"
    if (defined(invoker.dex_path)) {
      _dex_path = invoker.dex_path
    }
  }

  _template_name = target_name
  _accumulated_deps = []

  # write_build_config目标的名字
  _build_config_target_name = "${_template_name}__build_config"
  # .build_config文件的路径
  _build_config = "$_base_path.build_config"

  _accumulated_deps += [ ":$_build_config_target_name" ]
  write_build_config(_build_config_target_name) {
    if (defined(invoker.is_java_binary) && invoker.is_java_binary) {
      type = "java_binary"
    } else {
      type = "java_library"
    }
    requires_android = _requires_android
    supports_android = _supports_android
    build_config = _build_config
    java_sources_file = _java_sources_file
    jar_path = _jar_path
  }

  _srcjars = []
  if (defined(invoker.srcjars)) {
    _srcjars = invoker.srcjars
  }

  _compile_jar_target = "${target_name}__compile_java"
  _final_deps += [ ":$_compile_jar_target" ]
  compile_java(_compile_jar_target) {
    forward_variables_from(invoker, 
                          [
                            "additional_files",
                            "classpath",
                          ])
    deps = _accumulated_deps
    jar_path = _jar_path
    java_files = _java_files
    if (_java_files != []) {
      java_sources_file = _java_sources_file
    }
    build_config = _build_config
    srcjars = _srcjars
    supports_android = _supports_android
    requires_android = _requires_android
  }

  group(target_name) {
    public_deps = _final_deps
  }
}
