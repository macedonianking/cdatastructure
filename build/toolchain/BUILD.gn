toolchain("gcc_toolchain") {
  lib_switch = "-l"
  lib_dir_switch = "-L"

  tool("asm") {
    command = "gcc -c {{asmflags}} {{defines}} -o {{output}} {{source}}"
    outputs = [
      "{{source_out_dir}}/{{label_name}}{{source_name_part}}.o",
    ]
    description = "ASM {{source}}"
  }

  tool("cc") {
    depfile = "{{output}}.d"
    command = "gcc -MMD -MF ${depfile} -c {{cflags}} {{cflags_c}} {{defines}} {{include_dirs}} -o {{output}} {{source}}"
    depsformat = "gcc"
    outputs = [
      "{{source_out_dir}}/{{label_name}}{{source_name_part}}.o",
    ]
    description = "CC {{source}}"
  }

  tool("alink") {
    command = "ar rcs {{output}} {{inputs}}"
    description = "ALINK {{label}}"
    outputs = [
      "{{output_dir}}/{{target_output_name}}{{output_extension}}",
    ]

    output_prefix = "lib"
    default_output_dir = "{{root_out_dir}}"
    default_output_extension = ".a"
  }

  tool("solink") {
    command =
        "gcc -shared {{ldflags}} -o {{output}} {{inputs}} {{libs}} {{solibs}}"
    description = "SOLINK {{label}}"
    outputs = [
      "{{output_dir}}/{{target_output_name}}{{output_extension}}",
    ]

    output_prefix = "lib"
    default_output_dir = "{{root_out_dir}}"
    default_output_extension = ".so"
  }

  tool("link") {
    cmds = "gcc {{ldflags}} -o {{output}} @{{output}}.rsp"
    command = cmds
    description = "LINK {{label}}"
    rspfile = "{{output}}.rsp"
    rspfile_content = "{{inputs}} {{libs}} {{solibs}}"
    outputs = [
      "{{output_dir}}/{{target_output_name}}{{output_extension}}",
    ]

    default_output_dir = "{{root_out_dir}}"
    default_output_extension = ".exe"
  }

  tool("copy") {
    command = "cp -rf {{source}} {{output}}"
    description = "COPY {{label}}"
  }

  tool("stamp") {
    command = "touch {{output}}"
    description = "TOUCH {{label}}"
  }
}
